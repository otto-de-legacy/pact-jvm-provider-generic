apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'de.otto.pact-jvm'
version = '0.2.0-SNAPSHOT'


task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}


test {
    description "Tell gradle to use TestNG with all groups"
    useTestNG()
    options {
        useDefaultListeners = true
    }
}

repositories {
    mavenCentral()
    maven { url "http://nexus.lhotse.ov.otto.de:8080/content/groups/public" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile("au.com.dius:pact-jvm-provider_2.11:2.2.0")
    compile("com.google.guava:guava:18.0")
    compile("com.ning:async-http-client:1.9.26")

    testCompile("org.testng:testng:6.9.4")
    testCompile("org.hamcrest:hamcrest-core:1.3")
    testCompile("org.hamcrest:hamcrest-library:1.3")
    testCompile("org.mockito:mockito-core:1.10.19")
}

idea {
    project {
        languageLevel = '1.8'
    }
}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                packaging 'jar'
                name 'pact-jvm-provider-generic'
                description 'lightweight wrapper around pact-jvm-provider for easier use in java projects'
                url 'http://github.com/otto-de/pact-jvm-provider-generic'

                scm {
                    url 'scm:git@github.com:otto-de/pact-jvm-provider-generic.git'
                    connection 'scm:git@github.com:otto-de/pact-jvm-provider-generic.git'
                    developerConnection 'scm:git@github.com:otto-de/pact-jvm-provider-generic.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                 developers {
                    developer {
                        id 'pact-jvm-provider-generic-developers'
                        name 'Developers Developers Developers'
                    }
                }

            }
        }
    }

}
